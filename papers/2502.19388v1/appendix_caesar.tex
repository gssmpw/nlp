\subsection{Inputs to \toolcaesar}
\label{app:caesar_inputs}
%

\paragraph{The Monte Carlo $\pi$-approximator}
\phantom{a}

\begin{lstlisting}[language=C++, basicstyle=\ttfamily\small]
	coproc monte_carlo_pi(M : UReal) 
			-> (x : UReal, y : UReal, count : UReal)
	pre 0.85*(M)     
	post count
	{
		var i : UReal = 1
		count = 0
		@invariant(count + ite(0 <= i && i<=M, 0.85*((M-i) + 1), 0))
		while i <= M {
			
			var N : UInt = 16; 
			var j : UInt = unif(0, 15); //discrete_uniform(16)
			
			// --- Nondeterministic assignment x := [...]
			cohavoc x; 
			coassume ?!(j / N <= x && x <= (j+1) / N)
			// ---
			
			
			j  = unif(0, 15); //discrete_uniform(16)
			
			// --- Nondeterministic assignment y := [...]
			cohavoc y; 
			coassume ?!(j / N <= y && y <= (j+1) / N)
			// ---
			
			if x*x + y*y <= 1 {
				count = count +1
			}else{}
			
			i = i+1
			
		}
		
	}
\end{lstlisting}


\paragraph{Irwin-Hall without conditioning}
\phantom{a}
%
%
\begin{lstlisting}[language=C++, basicstyle=\ttfamily\small]
	coproc irwin_hall(M : UReal) -> (x : UReal)
	pre 1.1*((M)/2)
	post x
	{
		x = 0
		var i : UReal = 1
		@invariant(ite(i <= M, (x + 1.1*((M-i) + 1)/2), x))
		while i <= M {
			
			var inc : UReal; var N : UInt = 10; 
			var j : UInt = unif(0, 9); //discrete_uniform(10)
			
			// --- Nondeterministic assignment inc := [...]
			cohavoc inc; 
			coassume ?!(j / N <= inc && inc <= (j+1) / N)
			// ---
			
			x = x + inc
			i = i + 1
		}
	}
\end{lstlisting}


\paragraph{Irwin-Hall with conditioning}
\phantom{a}

\begin{lstlisting}[language=C++, basicstyle=\ttfamily\small]
	domain Exponentials {
		func exp(exponent: UReal): EUReal
		
		axiom exp_base exp(0) == 1
		axiom exp_step forall exponent: UReal. 
			exp(exponent + 1) == 0.5 * exp(exponent)
		axiom exp_antitone forall exp1: UReal. forall exp2: UReal. 
			(exp1 <= exp2) ==> (exp(exp2) <= exp(exp1))
	}
	
	
	proc irwin_hall_conditioning_wlp(M : UReal) -> (x : UReal)
	pre exp(M)
	post 1
	{
		x = 0
		var i : UReal = 1
		@invariant(ite(i <= M, exp((M-i) + 1), 1))
		while i <= M {
			var inc : UReal; var N : UInt = 2; 
			var j : UInt = unif(0, 1); //discrete_uniform(2)
			
			// --- Nondeterministic assignment inc := [...] 
			havoc inc; 
			assume ?(j / N <= inc && inc <= (j+1) / N) 
			// ---
			
			assert ?(inc <= 1/2) // observe
			x = x + inc
			i = i + 1
		}
	}
	

	coproc irwin_hall_conditioning_wp(M : UReal) -> (x : UReal)
	pre 1.5*((M)/8)
	post x
	{
		x = 0
		var i : UReal = 1
		@invariant(ite(i <= M, (x + 1.5*((M-i) + 1)/8), x))
		while i <= M {
			var inc : UReal; var N : UInt = 20; 
			var j : UInt = unif(0, 19); //discrete_uniform(20)
			
			// --- Nondeterministic assignment inc := [...] 
			cohavoc inc; 
			coassume ?!(j / N <= inc && inc <= (j+1) / N)
			// ---
			
			assert ?(inc <= 1/2) // observe
			x = x + inc
			i = i + 1
		}
	}
\end{lstlisting}


\paragraph{Probably Diverging Loop}
\phantom{a}

\begin{lstlisting}[language=C++, basicstyle=\ttfamily\small]
	domain Exponentials {
		func exp(exponent: UReal): EUReal
		
		axiom exp_base exp(0) == 1
		axiom exp_step forall exponent: UReal. 
			exp(exponent + 1) == 0.5 * exp(exponent)
		axiom exp_antitone forall exp1: UReal. forall exp2: UReal. 
			(exp1 <= exp2) ==> (exp(exp2) <= exp(exp1))
	}
	
	
	proc diverging(x_init : UReal, a : UReal, b : UReal) -> ()
	pre [a<=b]*(1-exp(x_init))
	post 0
	{
		var x : UReal = x_init 
		
		@invariant([a<=b]*(1-exp(x)))
		while(x >= 0){
			var y : UReal
			var N : UInt = 2; 
			var i : UInt = unif(0, 1); //discrete_uniform(2)
			
			// --- Nondeterministic assignment y := [...] 
			havoc y; 
			assume ?(i / N <= y && y <= (i+1) / N)
			// ---
			
			y = (b-a)*y + a        
			
			if (y <= (a+b)/2) {
				// --- diverge
				assert 1
				assume ?(false)
				// ---
			}else {}
			
			x = x - 1
			
		}
		
	}
\end{lstlisting}

\paragraph{Race between Tortoise and Hare}
\phantom{a}

\begin{lstlisting}[language=C++, basicstyle=\ttfamily\small]
	coproc tortoise_hare(h_init : UReal, t_init : UReal) 
			-> (count : UReal)
	pre 1.5*((t_init - h_init) + 2)*2 
	post count
	{
		var h : UReal = h_init
		var t : UReal = t_init
		
		count = 0
		
		@invariant(ite(h<=t, count + 1.5*((t-h) + 2)*2, count))
		while h <= t {
			var choice : Bool = flip(0.5)
			if choice {
				
				var inc : UReal; 
				var N : UInt = 25; 
				var j : UInt = unif(0, 24); //discrete_uniform(25)
				
				// --- Nondeterministic assignment inv := [...] 
				cohavoc inc; 
				coassume ?!(j / N <= inc && inc <= (j+1) / N)
				// ---
				
				inc = (10-0)*inc + 0 
				h = h + inc
			} else {}
			
			t = t+1
			count = count + 1
		}
	}
\end{lstlisting}
